buildscript {
    repositories {
        maven {
            url 'https://arti.tw.ee/artifactory/libs-release'
        }
        mavenLocal()
    }
    dependencies {
        classpath 'com.google.protobuf:protobuf-gradle-plugin:0.8.12'
        classpath "com.avast.gradle:gradle-docker-compose-plugin:0.10.9"
    }
}

plugins {
    id 'java'
    id 'idea'
    id 'org.springframework.boot' version '2.2.6.RELEASE' apply false
    id "com.google.protobuf" version "0.8.12"
}

apply plugin: 'docker-compose'

java {
    sourceCompatibility(JavaVersion.VERSION_1_8)
    targetCompatibility(JavaVersion.VERSION_1_8)
}

repositories {
    maven {
        url 'https://arti.tw.ee/artifactory/libs-release'
    }
    mavenLocal()
}

dependencies {
    annotationProcessor platform("org.springframework.boot:spring-boot-dependencies:2.2.6.RELEASE")
    implementation platform("org.springframework.boot:spring-boot-dependencies:2.2.6.RELEASE")
    compileOnly platform("org.springframework.boot:spring-boot-dependencies:2.2.6.RELEASE")
    testAnnotationProcessor platform("org.springframework.boot:spring-boot-dependencies:2.2.6.RELEASE")

    annotationProcessor 'org.projectlombok:lombok'
    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
    testAnnotationProcessor 'org.projectlombok:lombok'

    compileOnly 'org.projectlombok:lombok'

    implementation 'org.springframework.boot:spring-boot-starter'
    implementation 'org.springframework:spring-jdbc'
    implementation 'org.springframework.kafka:spring-kafka'
    implementation 'org.apache.kafka:kafka-clients'
    implementation 'com.google.guava:guava:28.2-jre'
    implementation 'com.google.protobuf:protobuf-java:3.11.4'
    implementation 'com.transferwise.common:tw-graceful-shutdown-interfaces:1.3.2'
    implementation 'com.transferwise.common:tw-base-utils:1.2.8'
    implementation 'com.transferwise.common:tw-leader-selector:1.1.2'
    implementation 'org.apache.curator:curator-recipes:4.2.0'
    implementation 'jakarta.validation:jakarta.validation-api'
    implementation 'org.apache.commons:commons-lang3'

    runtimeOnly 'com.transferwise.common:tw-graceful-shutdown:1.2.4'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.boot:spring-boot-starter-web'
    testImplementation 'org.springframework.boot:spring-boot-starter-actuator'
    testImplementation 'org.springframework.boot:spring-boot-starter-jdbc'
    testImplementation 'org.projectlombok:lombok'
    testImplementation 'org.awaitility:awaitility:4.0.2'
    testRuntimeOnly 'org.flywaydb:flyway-core'
    testRuntimeOnly 'org.mariadb.jdbc:mariadb-java-client'
    testRuntimeOnly 'org.postgresql:postgresql'
}

protobuf {
    protoc {
        artifact = 'com.google.protobuf:protoc:3.11.4'
    }
}

dockerCompose.isRequiredBy(test)

dockerCompose {
    useComposeFiles = ["src/test/resources/docker-compose.yml"]
    // Create some flakiness on slower comps
    // waitForTcpPorts = false
}

test {
    useJUnitPlatform()
    doFirst {
        dockerCompose.exposeAsEnvironment(test)
    }
}